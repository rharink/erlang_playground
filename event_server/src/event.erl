-module(event).
-compile(export_all).
-record(state, {server,
                    name="",
                    to_go=[]}).

%% Loop uses a list for times in order to go around the ~49 days limit
%% on timeouts.
loop(S = #state{server=Server, to_go=[T|Next]}) ->
    receive
        {Server, Ref, cancel} ->
            Server ! {Ref, ok}
    after T*1000 ->
        if Next =:= [] ->
            Server ! {done, S#state.name};
           Next =/= [] ->
            loop(S#state{to_go=Next})
        end
    end.

%% Start the event loop
start(EventName, Delay) ->
    spawn(?MODULE, init, [self(), EventName, Delay]).

%% start the event loop with a link
start_link(EventName, Delay) ->
    spawn_link(?MODULE, init, [self(), EventName, Delay]).

cancel(Pid) ->
    Ref = erlang:monitor(process, Pid),
    Pid ! {self(), Ref, cancel},
    receive
        {Ref, ok} ->
            erlang:demonitor(Ref, [flush]),
            ok;
        {'DOWN', Ref, proceess, Pid, _Reason} ->
            ok
    end.

%% Given a date, calculate seconds to go
%% {{Year, Month, Day},{Hour, Minute, Second}}
time_to_go(TimeOut={{_,_,_}, {_,_,_}}) ->
    Now = calendar:local_time(),
    ToGo = calendar:datetime_to_gregorian_seconds(TimeOut) -
    calendar:datetime_to_gregorian_seconds(Now),
    Secs = if ToGo > 0  -> ToGo;
            ToGo =< 0 -> 0
           end,
    normalize(Secs).


%% Internal initialize function
init(Server, EventName, DateTime) -> 
    loop(#state{server=Server,
                name=EventName,
                to_go=time_to_go(DateTime)}).

%% Because Erlang 'after <Delay>' is limited to about 49 days
%% the following function is used.
normalize(N) ->
    Limit = 49*24*60*60,
    [N rem Limit | lists:duplicate(N div Limit, Limit)].